Proxima.io = {}

Proxima.io.cliString = null
Proxima.io.cliEntries = []

Proxima.io.previousCommands = []

Proxima.io.message = {}
Proxima.io.message.line = "+----------------------------------------------------+"

// ------------------------------------------------------------------------------------------------------------

Proxima.io.message.colorize = function (txt, color)
    return "<color=" + color + ">" + txt + "</color>"
end function

Proxima.io.message.info = function (txt)
    return self.colorize("[i] " + txt, "#0091FF")
end function

Proxima.io.message.progress = function (txt)
    return self.colorize("[*] " + txt, "#00FFBB")
end function

Proxima.io.message.warning = function (txt)
    return self.colorize("[!] " + txt, "#FFDD00")
end function

Proxima.io.message.error = function (txt)
    return self.colorize("[!] " + txt, "#FF1548")
end function

Proxima.io.message.success = function (txt)
    return self.colorize("[+] " + txt, "#08FF57")
end function

// ------------------------------------------------------------------------------------------------------------

Proxima.io.addInputEntry = function (userCommand)
    self.cliEntries.push(self.cliString + userCommand)
end function

Proxima.io.addOutputEntry = function (outputString)
    self.cliEntries.push(outputString)
    return outputString
end function

Proxima.io.addProgressBar = function ()
    self.cliEntries.push("[###################################]==[ 100% ]")
end function

Proxima.io.addTransferProcess = function (fileSize)
    if fileSize.to_int >= 1000000000 then
        fileSizeGB = round((fileSize.to_int / 1000000000) * 10) / 10
        if fileSizeGB == round(fileSizeGB) then
            fileSizeGB = fileSizeGB + ".0 GB"
        else
            fileSizeGB = fileSizeGB + " GB"
        end if
        self.cliEntries.push("100%    " + str(floor(rnd * (900 - 50 + 1) + 50)) + "." + str(floor(rnd * (9 - 1 + 1) + 1)) + " KB/s" + "    0 sec (" + fileSizeGB + " of " + fileSizeGB + " copied)")
    else if fileSize.to_int >= 1000 then
        fileSizeMB = round((fileSize.to_int / 1000000) * 10) / 10 
        if fileSizeMB == round(fileSizeMB) then
            fileSizeMB = fileSizeMB + ".0 MB"
        else
            fileSizeMB = fileSizeMB + " MB"
        end if
        self.cliEntries.push("100%    " + str(floor(rnd * (900 - 50 + 1) + 50)) + "." + str(floor(rnd * (9 - 1 + 1) + 1)) + " KB/s" + "    0 sec (" + fileSizeMB + " of " + fileSizeMB + " copied)")
    else
        fileSize = fileSize + " Bytes"
        self.cliEntries.push("100%    " + str(floor(rnd * (900 - 50 + 1) + 50)) + "." + str(floor(rnd * (9 - 1 + 1) + 1)) + " KB/s" + "    0 sec (" + fileSize + " of " + fileSize + " copied)")
    end if
    self.cliEntries.push("Processing...")
end function

Proxima.io.clearScreen = function ()
    clear_screen
    if self.cliEntries.len > 0 then
        for cliEntry in self.cliEntries
            print(cliEntry)
        end for
    end if
end function

// ------------------------------------------------------------------------------------------------------------

Proxima.io.printHeader = function ()
    print(self.addOutputEntry(self.message.colorize("<b>              _______         .__                </b>", "#FC8403")))
    print(self.addOutputEntry(self.message.colorize("<b>_____________ \   _  \ ___  __|__| _____ _____   </b>", "#FC8403")))
    print(self.addOutputEntry(self.message.colorize("<b>\____ \_  __ \/  /_\  \\  \/  /  |/     \\__  \  </b>", "#FC8403")))
    print(self.addOutputEntry(self.message.colorize("<b>|  |_> >  | \/\  \_/   \>    <|  |  Y Y  \/ __ \_</b>", "#FC8403")))
    print(self.addOutputEntry(self.message.colorize("<b>|   __/|__|    \_____  /__/\_ \__|__|_|  (____  /</b>", "#FC8403")))
    print(self.addOutputEntry(self.message.colorize("<b>|__|                 \/      \/        \/     \/ </b>", "#FC8403")))
    print(self.addOutputEntry(""))
    print(self.addOutputEntry(self.message.colorize("<b>pr0xima â€” v" + Proxima.globals.version + "</b>", "#FFC466")))
    print(self.addOutputEntry(self.message.colorize("<b>A simple easy-to-use multi purpose hacking tool.</b>", "#FFFFFF")))
end function

Proxima.io.throwInputError = function (cmdMessage = null, cmdUsage = null)
    if cmdMessage != null then 
        if cmdUsage != null then
            print(self.addOutputEntry(self.message.error(cmdMessage + " Usage: " + cmdUsage)))
        else
            print(self.addOutputEntry(self.message.error(cmdMessage)))
        end if
    else
        print(self.addOutputEntry(self.message.error("Unknown command! Type <b>help</b> to list available commands.")))
    end if
end function