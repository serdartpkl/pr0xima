Proxima.io = {}

Proxima.io.inputString = null
Proxima.io.ioEntries = []

Proxima.io.previousInputs = []

Proxima.io.output = {}
Proxima.io.output.line = "+----------------------------------------------------+"

// ------------------------------------------------------------------------------------------------------------

Proxima.io.output.colorize = function (txt, color)
    return "<color=" + color + ">" + txt + "</color>"
end function

Proxima.io.output.info = function (txt)
    return self.colorize("[i] " + txt, "#0091FF")
end function

Proxima.io.output.progress = function (txt)
    return self.colorize("[*] " + txt, "#00FFBB")
end function

Proxima.io.output.warning = function (txt)
    return self.colorize("[!] " + txt, "#FFDD00")
end function

Proxima.io.output.error = function (txt)
    return self.colorize("[!] " + txt, "#FF1548")
end function

Proxima.io.output.success = function (txt)
    return self.colorize("[+] " + txt, "#08FF57")
end function

// ------------------------------------------------------------------------------------------------------------

Proxima.io.addInputEntry = function (userCommand)
    self.ioEntries.push(self.cliString + userCommand)
end function

Proxima.io.addOutputEntry = function (outputString)
    self.ioEntries.push(outputString)
    return outputString
end function

Proxima.io.addProgressBar = function ()
    self.ioEntries.push("[###################################]==[ 100% ]")
end function

Proxima.io.addTransferProcess = function (fileSize)
    if fileSize.to_int >= 1000000000 then
        fileSizeGB = round((fileSize.to_int / 1000000000) * 10) / 10
        if fileSizeGB == round(fileSizeGB) then
            fileSizeGB = fileSizeGB + ".0 GB"
        else
            fileSizeGB = fileSizeGB + " GB"
        end if
        self.ioEntries.push("100%   " + str(floor(rnd * (900 - 50 + 1) + 50)) + "." + str(floor(rnd * (9 - 1 + 1) + 1)) + " KB/s" + "   0 sec (" + fileSizeGB + " of " + fileSizeGB + " copied)")
    else if fileSize.to_int >= 1000 then
        fileSizeMB = round((fileSize.to_int / 1000000) * 10) / 10 
        if fileSizeMB == round(fileSizeMB) then
            fileSizeMB = fileSizeMB + ".0 MB"
        else
            fileSizeMB = fileSizeMB + " MB"
        end if
        self.ioEntries.push("100%   " + str(floor(rnd * (900 - 50 + 1) + 50)) + "." + str(floor(rnd * (9 - 1 + 1) + 1)) + " KB/s" + "   0 sec (" + fileSizeMB + " of " + fileSizeMB + " copied)")
    else
        fileSize = fileSize + " Bytes"
        self.ioEntries.push("100%   " + str(floor(rnd * (900 - 50 + 1) + 50)) + "." + str(floor(rnd * (9 - 1 + 1) + 1)) + " KB/s" + "   0 sec (" + fileSize + " of " + fileSize + " copied)")
    end if
    self.ioEntries.push("Processing...")
end function

Proxima.io.clearScreen = function ()
    clear_screen
    if self.ioEntries.len > 0 then
        for cliEntry in self.ioEntries
            print(cliEntry)
        end for
    end if
end function

// ------------------------------------------------------------------------------------------------------------

Proxima.io.printHeader = function ()                     
    print(self.addOutputEntry(self.output.colorize("<b>              _______         .__                </b>", "#FC8403")))
    print(self.addOutputEntry(self.output.colorize("<b>_____________ \   _  \ ___  __|__| _____ _____   </b>", "#FC8403")))
    print(self.addOutputEntry(self.output.colorize("<b>\____ \_  __ \/  /_\  \\  \/  /  |/     \\__  \  </b>", "#FC8403")))
    print(self.addOutputEntry(self.output.colorize("<b>|  |_> >  | \/\  \_/   \>    <|  |  Y Y  \/ __ \_</b>", "#FC8403")))
    print(self.addOutputEntry(self.output.colorize("<b>|   __/|__|    \_____  /__/\_ \__|__|_|  (____  /</b>", "#FC8403")))
    print(self.addOutputEntry(self.output.colorize("<b>|__|                 \/      \/        \/     \/ </b>", "#FC8403")))
    print(self.addOutputEntry(""))
    print(self.addOutputEntry(self.output.colorize("<b>pr0xima â€” v" + Proxima.globals.version + "</b>", "#FFC466")))
    print(self.addOutputEntry(self.output.colorize("<b>A simple easy-to-use multi purpose hacking tool.</b>", "#FFFFFF")))
end function

Proxima.io.throwInputError = function (cmdMessage = null, cmdUsage = null)
    if cmdMessage != null then 
        if cmdUsage != null then
            print(self.addOutputEntry(self.output.error(cmdMessage + " Usage: " + cmdUsage)))
        else
            print(self.addOutputEntry(self.output.error(cmdMessage)))
        end if
    else
        print(self.addOutputEntry(self.output.error("Unknown command! Type <b>help</b> to list available commands.")))
    end if
end function